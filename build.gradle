plugins {
    id "com.jfrog.bintray" version "1.7"
}

ext {
    BuildNumber = System.getenv("TRAVIS_BUILD_NUMBER") ?: "X"
}

allprojects {
    group = 'com.asteroid.duck.osgi.test'
    version = "1.0.${BuildNumber}"
    println "${project.name}:${project.version}"
}

subprojects {
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'java'
    apply plugin: 'osgi'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testCompile 'junit:junit:4.+'
    }

    // publish to Maven
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }
}

task bintrayUploadAll {
    group 'publishing'
    description 'Call :bintrayUpload on all subprojects'
    subprojects.each {
        dependsOn("${it.path}:bintrayUpload")
    }
}



